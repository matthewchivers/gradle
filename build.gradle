subprojects {

    apply plugin: 'java-gradle-plugin'
    apply plugin: 'maven-publish'

    group = galasaGroup

    // If a plugin holds a different version to the root project,
    // <plugin>/gradle.properties should be given the property "pluginVersion"
    // e.g. pluginVersion=0.1.1
    if (project.hasProperty('pluginVersion')){
        version = pluginVersion
    } else {
        version = defaultPluginVersion
    }

    def distRepo = System.getenv('MAVEN_REPO') ?: "https://nexus.galasa.dev/repository/maven-development"

    tasks.withType(PublishToMavenRepository) {
        onlyIf { jar.didWork }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.apache.felix:org.apache.felix.bundlerepository:2.0.10'
        implementation 'org.reflections:reflections:0.9.11'
        implementation 'com.google.code.gson:gson:2.8.5'
        testImplementation 'junit:junit:4.13'
    }
    
    publishing {
        // publications {
        //     pluginMaven(MavenPublication) {
        //     }
        // }
        repositories {
            maven {
                name = 'distRepo'
                url  = distRepo
                credentials {
                    username System.getenv('MAVENUSERNAME')
                    password System.getenv('MAVENPASSWORD')
                }
            }
        }
    }

}


